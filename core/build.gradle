plugins {
    id "com.android.library"
    id "maven-publish"
    id "kotlin-android"
    id "kotlin-kapt"
    id "org.jetbrains.dokka"
}

apply from: "$project.rootDir/spotless.gradle"

android {
    def versionProperties = new Properties()
    rootProject.file("version.properties").withInputStream { versionProperties.load(it) }

    def getDynamicVersionCode =  { ->
        String major = versionProperties['v.major']
        String minor = versionProperties['v.minor']
        String hotfix = versionProperties['v.hotfix']
        String result = "$major$minor$hotfix"
        return result.toInteger()
    }

    def getVersionName = { ->
        String major = versionProperties['v.major']
        String minor = versionProperties['v.minor']
        String hotfix = versionProperties['v.hotfix']
        String result = "${major.toString()}.${minor.toString()}.${hotfix.toString()}"
        return result
    }

    def getArtifactId = { ->
        return rootProject.name
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        group = "com.zuko.billingz"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName getVersionName()
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    publishNonDefault true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'
    api 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'
    api "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    api "androidx.core:core-ktx:1.6.0"
    api "androidx.appcompat:appcompat:1.3.1"
    api "com.google.android.material:material:1.4.0"
    api 'androidx.constraintlayout:constraintlayout:2.1.0'
    api 'com.airbnb.android:lottie:4.1.0'

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task installGitHook(type: Copy) {
    println "Installing git hooks..."

    from new File(rootProject.rootDir, "scripts/pre-commit")
    into { new File(rootProject.rootDir, '.git/hooks') }

    fileMode 0775
}

tasks.getByName("preBuild").dependsOn installGitHook
